// --------------- Plugins ---------------

plugins {
	id 'java'
	id 'application'
	// https://plugins.gradle.org/plugin/com.github.johnrengelman.shadow
	id 'com.github.johnrengelman.shadow' version '5.2.0'
}

// --------------- Publication Info ---------------

group = 'org.fulib'
version = "git describe --tags".execute([], rootDir).text[1..-2] // strip v and \n
description = 'fulib.org Projects backend'

// --------------- General Settings ---------------

mainClassName = 'org.fulib.webapp.projects.Main'

shadowJar.archiveClassifier = 'all'

// --------------- Dependencies ---------------

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// --------------- Dagger (Dependency Injection) ---------------

	implementation 'com.google.dagger:dagger:2.35.1'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.35.1'

	// --------------- Other Libraries ---------------

	// https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

	// https://mvnrepository.com/artifact/com.sparkjava/spark-core
	implementation group: 'com.sparkjava', name: 'spark-core', version: '2.9.3'

	// https://mvnrepository.com/artifact/org.json/json
	implementation group: 'org.json', name: 'json', version: '20210307'

	// https://mvnrepository.com/artifact/org.mongodb/mongodb-driver-sync
	implementation group: 'org.mongodb', name: 'mongodb-driver-sync', version: '4.2.3'

	// https://mvnrepository.com/artifact/com.auth0/java-jwt
	implementation group: 'com.auth0', name: 'java-jwt', version: '3.15.0'

	testImplementation junitDependency

	// https://mvnrepository.com/artifact/org.mockito/mockito-core
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.9.0'

	// --------------- Docker (Projects) ---------------

	// https://mvnrepository.com/artifact/com.github.docker-java/docker-java-core
	implementation group: 'com.github.docker-java', name: 'docker-java-core', version: '3.2.8'

	// https://mvnrepository.com/artifact/com.github.docker-java/docker-java-transport-httpclient5
	implementation group: 'com.github.docker-java', name: 'docker-java-transport-httpclient5', version: '3.2.8'
}

// --------------- Version Injection ---------------

processResources {
	def version = project.version.toString()
	def config = project(':backend').configurations.compileClasspath
	def fulibVersion = getDependencyVersion(config, 'org.fulib', 'fulib')
	def fulibToolsVersion = getDependencyVersion(config, 'org.fulib', 'fulibTools')
	def fulibYamlVersion = getDependencyVersion(config, 'org.fulib', 'fulibYaml')
	def fulibTablesVersion = getDependencyVersion(config, 'org.fulib', 'fulibTables')
	def fulibScenariosVersion = getDependencyVersion(config, 'org.fulib', 'fulibScenarios')
	def fulibGradleVersion = getDependencyVersion(rootProject.buildscript.configurations.classpath, 'org.fulib', 'fulibGradle')
	def fulibMockupsVersion = getDependencyVersion(config, 'org.fulib', 'fulibMockups')

	from(rootProject.rootDir) {
		include 'gradle/wrapper/**', 'gradlew', 'gradlew.bat'
		into 'org/fulib/webapp/projects/zip'

		// we have to save the wrapper jar as a zip, see
		// https://imperceptiblethoughts.com/shadow/configuration/dependencies/#embedding-jar-files-inside-your-shadow-jar
		rename 'gradle-wrapper.jar', 'gradle-wrapper.jar.zip'
	}

	filesMatching([
			'org/fulib/webapp/projects/zip/build.gradle',
	]) {
		filter {
			it = it.replace('$$version$$', version)
			it = it.replace('$$fulibVersion$$', fulibVersion)
			it = it.replace('$$fulibToolsVersion$$', fulibToolsVersion)
			it = it.replace('$$fulibYamlVersion$$', fulibYamlVersion)
			it = it.replace('$$fulibTablesVersion$$', fulibTablesVersion)
			it = it.replace('$$fulibScenariosVersion$$', fulibScenariosVersion)
			it = it.replace('$$fulibMockupsVersion$$', fulibMockupsVersion)
			it = it.replace('$$fulibGradleVersion$$', fulibGradleVersion)
			it
		}
	}
}

String getDependencyVersion(Configuration config, String group, String name) {
	config.resolvedConfiguration.resolvedArtifacts.findResult {
		def id = it.moduleVersion.id
		id.group == group && id.name == name ? id.version : null
	}
}
