// --------------- Plugins ---------------

plugins {
	id 'java'
	id 'application'
	// https://plugins.gradle.org/plugin/com.github.johnrengelman.shadow
	id 'com.github.johnrengelman.shadow' version '5.2.0'
}

// --------------- Publication Info ---------------

group = 'org.fulib'
version = "git describe --tags".execute([], rootDir).text[1..-2] // strip v and \n
description = 'The fulib.org website and fulibScenarios Web App'

// --------------- General Settings ---------------

mainClassName = 'org.fulib.webapp.Main'

shadowJar.archiveClassifier = 'all'

// --------------- Dependencies ---------------

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// --------------- Dagger (Dependency Injection) ---------------

	// https://mvnrepository.com/artifact/com.google.dagger/dagger
	implementation 'com.google.dagger:dagger:2.42'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.42'

	// --------------- Fulib Libraries ---------------

	implementation fulibDependency
	implementation fulibScenariosDependency
	implementation fulibMockupsDependency
	implementation fulibTablesDependency

	// --------------- Other Libraries ---------------

	implementation slf4jDependency

	// https://mvnrepository.com/artifact/commons-io/commons-io
	implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

	// https://mvnrepository.com/artifact/com.sparkjava/spark-core
	implementation group: 'com.sparkjava', name: 'spark-core', version: '2.9.3'

	// https://mvnrepository.com/artifact/org.json/json
	implementation group: 'org.json', name: 'json', version: '20220320'

	// https://mvnrepository.com/artifact/org.mongodb/mongodb-driver-sync
	implementation group: 'org.mongodb', name: 'mongodb-driver-sync', version: '4.6.0'

	// https://mvnrepository.com/artifact/com.auth0/java-jwt
	implementation group: 'com.auth0', name: 'java-jwt', version: '3.19.2'

	// compile instead of testCompile because the web service uses it
	implementation junitDependency

	testImplementation slf4jDependency

	// https://mvnrepository.com/artifact/org.mockito/mockito-core
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.5.1'

	// --------------- CommonMark ---------------

	// https://mvnrepository.com/artifact/org.commonmark/commonmark
	implementation group: 'org.commonmark', name: 'commonmark', version: '0.18.2'

	// https://mvnrepository.com/artifact/org.commonmark/commonmark-ext-gfm-tables
	implementation group: 'org.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.18.2'

	// https://mvnrepository.com/artifact/org.commonmark/commonmark-ext-autolink
	implementation group: 'org.commonmark', name: 'commonmark-ext-autolink', version: '0.18.2'

	// https://mvnrepository.com/artifact/org.commonmark/commonmark-ext-gfm-strikethrough
	implementation group: 'org.commonmark', name: 'commonmark-ext-gfm-strikethrough', version: '0.18.2'

	// https://mvnrepository.com/artifact/org.commonmark/commonmark-ext-task-list-items
	implementation group: 'org.commonmark', name: 'commonmark-ext-task-list-items', version: '0.18.2'
}

// --------------- Version Injection ---------------

processResources {
	def version = project.version.toString()
	def config = configurations.compileClasspath
	def fulibVersion = getDependencyVersion(config, 'org.fulib', 'fulib')
	def fulibToolsVersion = getDependencyVersion(config, 'org.fulib', 'fulibTools')
	def fulibYamlVersion = getDependencyVersion(config, 'org.fulib', 'fulibYaml')
	def fulibTablesVersion = getDependencyVersion(config, 'org.fulib', 'fulibTables')
	def fulibScenariosVersion = getDependencyVersion(config, 'org.fulib', 'fulibScenarios')
	def fulibGradleVersion = getDependencyVersion(rootProject.buildscript.configurations.classpath, 'org.fulib', 'fulibGradle')
	def fulibMockupsVersion = getDependencyVersion(config, 'org.fulib', 'fulibMockups')
	def junitVersion = getDependencyVersion(config, 'junit', 'junit')
	def slf4jVersion = getDependencyVersion(config, 'org.slf4j', 'slf4j-simple')

	from(rootProject.rootDir) {
		include 'gradle/wrapper/**', 'gradlew', 'gradlew.bat'
		into 'org/fulib/webapp/projectzip'

		// we have to save the wrapper jar as a zip, see
		// https://imperceptiblethoughts.com/shadow/configuration/dependencies/#embedding-jar-files-inside-your-shadow-jar
		rename 'gradle-wrapper.jar', 'gradle-wrapper.jar.zip'
	}

	filesMatching(['**/*.gradle', '**/*.properties']) {
		filter {
			it = it.replace('$$version$$', version)
			it = it.replace('$$fulibVersion$$', fulibVersion)
			it = it.replace('$$fulibToolsVersion$$', fulibToolsVersion)
			it = it.replace('$$fulibYamlVersion$$', fulibYamlVersion)
			it = it.replace('$$fulibTablesVersion$$', fulibTablesVersion)
			it = it.replace('$$fulibScenariosVersion$$', fulibScenariosVersion)
			it = it.replace('$$fulibMockupsVersion$$', fulibMockupsVersion)
			it = it.replace('$$fulibGradleVersion$$', fulibGradleVersion)
			it = it.replace('$$fulibWorkflowsVersion$$', fulibWorkflowsDependency.version)
			it = it.replace('$$junitVersion$$', junitVersion)
			it = it.replace('$$slf4jVersion$$', slf4jVersion)
			it
		}
	}
}

String getDependencyVersion(Configuration config, String group, String name) {
	config.resolvedConfiguration.resolvedArtifacts.findResult {
		def id = it.moduleVersion.id
		id.group == group && id.name == name ? id.version : null
	}
}
