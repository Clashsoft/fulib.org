// --------------- Plugins ---------------

plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.4'
	id 'de.clashsoft.simple-publish' version '0.3.0'
	id 'com.github.johnrengelman.shadow' version '4.0.4'
}

// --------------- Publication Info ---------------

group = 'org.fulib'
version = "git describe --tags".execute().text[1..-2] // strip v and \n
description = 'The fulib.org website and Fulib Scenarios Web App'

publishInfo {
	websiteUrl = 'https://www.fulib.org/'
	issueTrackerUrl = 'https://github.com/fujaba/fulib.org/issues'
	vcsUrl = 'https://github.com/fujaba/fulib.org'
	githubRepo = 'fujaba/fulib.org'
	labels = [ 'fulib', 'scenarios', 'webapp', 'website' ]

	license {
		shortName = 'BSD 3-Clause'
		longName = 'BSD 3-Clause "New" or "Revised" License'
		url = 'https://opensource.org/licenses/BSD-3-Clause'
	}

	developer {
		id = 'clashsoft'
		name = 'Adrian Kunz'
		email = 'clashsoft@hotmail.com'
	}

	developer {
		id = 'azuendorf'
		name = 'Albert ZÃ¼ndorf'
		email = 'zuendorf@uni-kassel.de'
	}
}

// --------------- General Settings ---------------

mainClassName = 'org.fulib.scenarios.WebService'

sourceCompatibility = 1.8

shadowJar.archiveClassifier = 'all'

// --------------- Dependencies ---------------

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	// --------------- Fulib Libraries ---------------

	compile group: 'org.fulib', name: 'fulibScenarios', version: '[0.8.0,)'

	compile group: 'org.fulib', name: 'fulibMockups', version: '[0.1.0,)'

	// --------------- Other Libraries ---------------

	// https://mvnrepository.com/artifact/org.slf4j/slf4j-nop
	compile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.26'

	// https://mvnrepository.com/artifact/com.sparkjava/spark-core
	compile group: 'com.sparkjava', name: 'spark-core', version: '2.9.1'

	// https://mvnrepository.com/artifact/org.json/json
	compile group: 'org.json', name: 'json', version: '20180813'

	// https://mvnrepository.com/artifact/org.mongodb/mongodb-driver-sync
	compile group: 'org.mongodb', name: 'mongodb-driver-sync', version: '3.11.0'

	// compile instead of testCompile because the web service uses it
	// https://mvnrepository.com/artifact/junit/junit
	compile group: 'junit', name: 'junit', version: '4.12'
}

// --------------- Version Injection ---------------

def fulibGradleVersion = '0.2.0'

processResources {
	filesMatching([ 'webapp/index.html', 'projectzip/build.gradle', 'org/fulib/webapp/version.properties' ]) {
		filter {
			it = it.replace('$$version$$', project.version.toString())
			it = it.replace('$$fulibScenariosVersion$$', getDependencyVersion(configurations.compile, 'org.fulib',
					'fulibScenarios'))
			it = it.replace('$$fulibMockupsVersion$$', getDependencyVersion(configurations.compile, 'org.fulib',
					'fulibMockups'))
			it = it.replace('$$fulibGradleVersion$$', fulibGradleVersion)
			it
		}
	}
}

String getDependencyVersion(Configuration config, String group, String name) {
	config.resolvedConfiguration.firstLevelModuleDependencies.findAll {
		it.moduleName == name && it.moduleGroup == group
	}[0].moduleVersion
}

// --------------- Project Zip Wrapper ---------------

wrapper.doLast {
	copy {
		from '.'
		include 'gradle/wrapper/**', 'gradlew', 'gradlew.bat'
		into 'src/main/resources/projectzip'

		// we have to save the wrapper jar as a zip, see
		// https://imperceptiblethoughts.com/shadow/configuration/dependencies/
		// "Embedding Jar Files Inside Your Shadow Jar"
		rename 'gradle-wrapper.jar', 'gradle-wrapper.jar.zip'
	}
}
